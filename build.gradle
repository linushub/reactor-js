/*
 * Copyright (c) 2011-2014 GoPivotal, Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor Data components'

ext {
	gradleVersion = '1.11'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Logging
	slf4jVersion = '1.7.6'
	logbackVersion = '1.1.1'

	// Spring
	springVersion = '4.0.3.RELEASE'

	// Reactor
	reactorVersion = '1.1.1.BUILD-SNAPSHOT'

	// Libraries
	openHftLangVersion = '6.1.4'
	openHftChronicleVersion = '2.0.3'
	jacksonDatabindVersion = '2.3.2'
	jsonPathVersion = '0.9.1'

	// Testing
	junitVersion = '4.11'
}

buildscript {
	repositories {
        jcenter()
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.1'
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
	}
}
repositories {
  jcenter()
}

apply from: "$gradleScriptDir/setup.gradle"

subprojects {
	apply plugin: 'propdeps'
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'eclipse'
	apply plugin: 'maven'

	configurations.all {
		exclude group: "commons-logging"
	}

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:serial",
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:-options", // intentionally disabled
			"-Xlint:-fallthrough", // intentionally disabled
			"-Xlint:-rawtypes", // TODO enable and fix warnings
			"-Xlint:-deprecation", // TODO enable and fix warnings
			"-Xlint:-unchecked" // TODO enable and fix warnings
	]

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	group = 'org.projectreactor.js'
	repositories {
		//mavenLocal()
		mavenCentral()
		maven { url 'http://repo.spring.io/libs-milestone' }
		maven { url 'http://repo.spring.io/libs-snapshot' }
	}

	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"

		provided "javax.annotation:javax.annotation-api:1.2"

		// Testing
		testCompile "junit:junit-dep:$junitVersion",
								"org.mockito:mockito-core:1.9.5",
								"org.hamcrest:hamcrest-library:1.3"
		testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	}
}

project('reactor-js-core') {
	apply plugin: 'me.champeau.gradle.jmh'
	description = 'Core Reactor components for working with Javascript in Reactor'
	
   	
	dependencies {
		// Reactor
		compile "org.projectreactor:reactor-core:$reactorVersion"
		

		// Hashing
		compile "com.sangupta:murmur:1.0.0"

		// JSON
		compile "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion",
						"com.jayway.jsonpath:json-path:$jsonPathVersion"
						
		// JMH benchmarking
		jmh "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
		jmhCompile "org.openjdk.jmh:jmh-generator-annprocess:0.9"
		// without below line eclipse complains
		compile "org.openjdk.jmh:jmh-generator-annprocess:0.9"

		// High-speed Messaging
		optional "net.openhft:lang:$openHftLangVersion",
						 "net.openhft:chronicle:$openHftChronicleVersion"

		testCompile project(":reactor-js-test")
		
	}
	jmh {
		iterations = 5
		warmupIterations = 10
		fork = 1
	}
	
	sourceSets {
	    jmh {
	        java {
	            srcDir 'src/jmh/java'
	        }
	        resources {
	            srcDir 'src/jmh/resources'
	        }
	    }
	}
}

project('reactor-js-rest') {
	description = 'Reactor components for working with Javascript REST services'

	dependencies {
		compile project(":reactor-js-core"),
						"org.projectreactor:reactor-net:$reactorVersion"
	}
}

project('reactor-js-test') {
	description = 'Reactor components for working with Javascript tests'

	dependencies {
		// JUnit
		compile "junit:junit-dep:$junitVersion"
	}
}


